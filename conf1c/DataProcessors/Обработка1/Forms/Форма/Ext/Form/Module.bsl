
&НаКлиенте
Перем КомпонентаДляВнешнегоСобытия;

&НаКлиенте
Перем СчетчикВнешнихСобытий;

&НаКлиенте
Перем ДатаНачалаСобытий;


&НаКлиенте
Процедура Тест1(Команда)
	Тест1НаСервере(ИмяФайла);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура Тест1НаСервере(ИмяФайла)
	
	Начало = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	Если Не ПодключитьВнешнююКомпоненту(ИмяФайла, "Test", ТипВнешнейКомпоненты.Native, ТипПодключенияВнешнейКомпоненты.НеИзолированно) Тогда 
		Сообщить("Не удалось подключить");
		Возврат;
	КонецЕсли;
	
	Сообщить("Подключена");
	ОбъектКомпоненты = Новый ("AddIn.Test.Class1");
	Test = ОбъектКомпоненты.Test;
	Конец = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Сообщить(СтрШаблон("Test: %1", Test));
	Сообщить(СтрШаблон("Длительность: %1", Конец - Начало));
	
КонецПроцедуры


&НаКлиенте
Процедура Тест2(Команда)
	Тест2НаСервере(ИмяФайла);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура Тест2НаСервере(ИмяФайла) 
	
	Начало = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	Попытка
		ОбъектКомпоненты = Новый ("AddIn.Test.Class1");
	Исключение
		Если Не ПодключитьВнешнююКомпоненту(ИмяФайла, "Test", ТипВнешнейКомпоненты.Native, ТипПодключенияВнешнейКомпоненты.НеИзолированно) Тогда 
			ВызватьИсключение "Не удалось подключить";
		КонецЕсли;
		ОбъектКомпоненты = Новый ("AddIn.Test.Class1");
	КонецПопытки;
	
	ОбъектКомпоненты.PropI32 = 123;
	Если ОбъектКомпоненты.PropI32 <> 123 Тогда
		ВызватьИсключение "Не удалось установить значение PropI32";
	КонецЕсли; 
	
	ОбъектКомпоненты.PropF64 = 456.789;
	Если ОбъектКомпоненты.PropF64 <> 456.789 Тогда
		ВызватьИсключение "Не удалось установить значение PropF64";
	КонецЕсли;
	
	ОбъектКомпоненты.PropBool = Истина;
	Если ОбъектКомпоненты.PropBool <> Истина Тогда
		ВызватьИсключение "Не удалось установить значение PropBool";
	КонецЕсли;
	
	Date = ТекущаяДатаСеанса();
	ОбъектКомпоненты.PropDate = Date;
	Если ОбъектКомпоненты.PropDate <> Date Тогда
		ВызватьИсключение "Не удалось установить значение PropDate";
	КонецЕсли;
	
	ОбъектКомпоненты.PropStr = "Привет!";
	Если ОбъектКомпоненты.PropStr <> "Привет!" Тогда
		ВызватьИсключение "Не удалось установить значение PropStr";
	КонецЕсли;
	
	Blob = ПолучитьДвоичныеДанныеИзСтроки("Привет!");
	ОбъектКомпоненты.PropBlob = Blob;
	Если ОбъектКомпоненты.PropBlob <> Blob Тогда
		ВызватьИсключение "Не удалось установить значение PropBlob";
	КонецЕсли;
	
	Если ОбъектКомпоненты.Method1("11", "22", "33") <> "112233" Тогда
		ВызватьИсключение "Не удалось установить значение Method1";
	КонецЕсли;
	
	ВозвращаемаяСтрока = Неопределено;
	ВозвращаемоеЧисло = Неопределено;
	Если Не ОбъектКомпоненты.Method2(ВозвращаемаяСтрока, ВозвращаемоеЧисло) 
		Или ВозвращаемаяСтрока <> "Return value"
		Или ВозвращаемоеЧисло <> 1 Тогда
		ВызватьИсключение "Не удалось вызвать Method2";
	КонецЕсли;
	
	Конец = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Сообщить(СтрШаблон("Длительность: %1", Конец - Начало));
	
КонецПроцедуры

&НаКлиенте
Процедура Тест3(Команда)
	Тест3НаСервере(ИмяФайла);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура Тест3НаСервере(ИмяФайла)
	
	Попытка
		ОбъектКомпоненты2 = Новый ("AddIn.Test.Class2");
	Исключение
		Если Не ПодключитьВнешнююКомпоненту(ИмяФайла, "Test", ТипВнешнейКомпоненты.Native, ТипПодключенияВнешнейКомпоненты.НеИзолированно) Тогда 
			ВызватьИсключение "Не удалось подключить";
		КонецЕсли;
		ОбъектКомпоненты2 = Новый ("AddIn.Test.Class2");
	КонецПопытки;
	
	Если ОбъектКомпоненты2.Method1(111) <> 222 Тогда
		ВызватьИсключение "Не удалось вызвать Method1";
	КонецЕсли;
	
	// Вызвается метод не с тем типом
	Ошибка = Неопределено;
	Попытка
		ОбъектКомпоненты2.Method1("111");
	Исключение
		Ошибка = ОбъектКомпоненты2.LastError; 
	КонецПопытки;
	Если Ошибка <> "Incompatible type" Тогда
		ВызватьИсключение "При вызове компоненты не было нужной ошибки";
	КонецЕсли;
	
	Если ОбъектКомпоненты2.Method2(111, 222) <> 333 Тогда
		ВызватьИсключение "Не удалось вызвать Method2";	
	КонецЕсли;
	
	Если ОбъектКомпоненты2.Prop1 <> 333 Тогда
		ВызватьИсключение "Свойство Prop1 содержит неверное значение";
	КонецЕсли;
	Сообщить("Тест выполнен успешно");	
	
КонецПроцедуры

&НаКлиенте
Процедура Тест4(Команда)
	Тест4НаСервере(ИмяФайла);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура Тест4НаСервере(ИмяФайла)
	
	Попытка
		ОбъектКомпоненты2 = Новый ("AddIn.Test.Class2");
	Исключение
		Если Не ПодключитьВнешнююКомпоненту(ИмяФайла, "Test", ТипВнешнейКомпоненты.Native, ТипПодключенияВнешнейКомпоненты.НеИзолированно) Тогда 
			ВызватьИсключение "Не удалось подключить";
		КонецЕсли;
		ОбъектКомпоненты2 = Новый ("AddIn.Test.Class2");
	КонецПопытки;
	
	Начало = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Для К = 1 По 1000000 Цикл
		ОбъектКомпоненты2.Prop1 = 123;
	КонецЦикла;
	Конец = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Сообщить(СтрШаблон("Длительность: %1 мс", Конец - Начало));
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ТестВнешнееСобытие(Команда)
	
	Если Не Ждать ПодключитьВнешнююКомпонентуАсинх(ИмяФайла, "Test", ТипВнешнейКомпоненты.Native, ТипПодключенияВнешнейКомпоненты.НеИзолированно) Тогда 
		ВызватьИсключение "Не удалось подключить";
	КонецЕсли;
	
	КомпонентаДляВнешнегоСобытия = Новый ("AddIn.Test.Class2");
	
	СчетчикВнешнихСобытий = 0;
	ДатаНачалаСобытий = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Элементы.ТестВнешнееСобытие.Доступность = Ложь;
	
	Ждать КомпонентаДляВнешнегоСобытия.StartTimerAsync(1000);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Если Источник = "Class2" И Событие = "Timer" Тогда
		СчетчикВнешнихСобытий = СчетчикВнешнихСобытий + 1;
		Сообщить(СтрШаблон("%1 мс, внешнее событие № %2, данные: %3", ТекущаяУниверсальнаяДатаВМиллисекундах() - ДатаНачалаСобытий, СчетчикВнешнихСобытий, Данные));
		Если СчетчикВнешнихСобытий = 5 Тогда
			Ждать КомпонентаДляВнешнегоСобытия.StopTimerAsync();
		КонецЕсли;
	ИначеЕсли Источник = "Class2" И Событие = "TimerShutdown" Тогда
		КомпонентаДляВнешнегоСобытия = Неопределено;
		Сообщить(СтрШаблон("%1 мс, TimerShutdown", ТекущаяУниверсальнаяДатаВМиллисекундах() - ДатаНачалаСобытий));
		Элементы.ТестВнешнееСобытие.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры
